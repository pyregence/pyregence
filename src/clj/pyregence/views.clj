(ns pyregence.views
  (:require [clojure.data.json :as json]
            [hiccup.page :refer [html5 include-js]]))

(defn head []
  [:head
   [:title "Pyregence"]
   [:meta {:charset "utf-8"}]
   [:meta {:name "viewport" :content "width=device-width, initial-scale=1"}]
   [:meta {:name "description" :content (str "Pyregence is a web portal for displaying near-term fire forecast "
                                             "results for the state of California. The fire spread and impact maps "
                                             "are generated by running multiple state-of-the-art simulation models "
                                             "on a continuously updated stream of weather data.")}]
   [:meta {:name "keywords" :content "pyregence california fire forecast cec epic sig reax"}]
   (include-js "/cljs/app.js")])

(defn cljs-init [params]
  (let [js-params (json/write-str params)]
    [:script {:type "text/javascript"}
      ;; TODO, I dont think we need to pass params this way, unless we are having the server modify them.
      ;;       We can read them the same way they are read for figwheel on init (see client.cljs).
     (str "window.onload = function () { pyregence.client.init(" js-params "); };")]))

(defn render-page [valid?]
  (fn [request]
    {:status  (if valid? 200 404)
     :headers {"Content-Type" "text/html"}
     :body    (html5
               (head)
               [:body
                [:div#app]
                (cljs-init (:params request))])}))

(defn data-response
  ([status body]
   (data-response status body true))
  ([status body edn?]
   {:status  status
    :headers {"Content-Type" (if edn? "application/edn" "application/json")}
    :body    (if edn? (pr-str body) (json/write-str body))}))
